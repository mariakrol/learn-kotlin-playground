import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.7.20"
    id "io.qameta.allure" version "2.9.3"
    id "org.openapi.generator" version "6.2.1"
}

sourceCompatibility = "18"
targetCompatibility = "18"

group "com.makrol"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

[compileKotlin, compileTestKotlin].forEach {
    it.kotlinOptions {
        jvmTarget = "11"
    }
}

ext {
    allureVersion = "2.20.1"
    junitVersion = "5.9.1"
    hopliteVersion = "2.7.0"
    okhttpVersion = "2.7.5"
}

dependencies {
    //OpenApi generation
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:2.14.2")

    implementation("com.squareup.okhttp3:okhttp:4.10.0")
    implementation("com.squareup.okhttp3:logging-interceptor:4.9.3")

    //UI models implementation
    implementation("com.codeborne:selenide:6.10.1")

    //Email
    implementation("com.icegreen:greenmail-junit5:2.0.0-alpha-3")

    //Tests organization
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    //Assertions
    testImplementation("org.assertj:assertj-core:3.23.1")

    //Logging & Reporting
    implementation("io.github.microutils:kotlin-logging-jvm:3.0.4")
    implementation("org.slf4j:slf4j-simple:2.0.5")
    testImplementation("io.qameta.allure:allure-selenide:${allureVersion}")

    //configuration
    implementation("com.sksamuel.hoplite:hoplite-core:${hopliteVersion}")
    implementation("com.sksamuel.hoplite:hoplite-json:${hopliteVersion}")

    //Utilities
    implementation("org.jetbrains.kotlin:kotlin-reflect:1.7.20")
}

openApiGenerate {
    generatorName = "kotlin"
    inputSpec = "$rootDir/src/test/resources/teamCityApi.json"
    outputDir = "$buildDir/generated"
    apiPackage = "com.makrol.teamcity.api.swagger.client.api"
    invokerPackage = "com.makrol.teamcity.api.swagger.client.invoker"
    modelPackage = "com.makrol.teamcity.api.swagger.client.model"
    configOptions = [
            dateLibrary: "java8",
            library: "jvm-okhttp4",
            moshiCodeGen: "false",
            serializationLibrary: "jackson"
    ]
}

sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
    main.kotlin.srcDirs += "$buildDir/generated/src/main/kotlin"
}

tasks.withType(KotlinCompile).configureEach {
    dependsOn("openApiGenerate")
    kotlinOptions.jvmTarget = "11"
}

test {
    useJUnitPlatform()

    systemProperties = [
            'selenide.headless': 'true'
    ]
}
allure {
    version = "${allureVersion}"

    useJUnit5 {
        version = "${allureVersion}"
    }
}